namespace: Tone=Ttree.Neos.Tone
namespace: TypoScript=Neos.Fusion

include: Prototypes/**/*
include: RawContentMode/**/*

prototype(Ttree.Neos.Tone:TonePreviewStyleSheet) < prototype(TypoScript:Tag) {
    tagName = 'link'
    selfClosingTag = TRUE
    attributes {
        rel = 'stylesheet'
        media = 'all'
        href = TypoScript:ResourceUri {
            path = 'resource://Ttree.Neos.Tone/Public/Styles/Backend.css'
        }
    }
}

prototype(Neos.Neos:Page) {
    head {
        stylesheets {
            tone = Ttree.Neos.Tone:TonePreviewStyleSheet {
                @position = 'end'
            }
        }
    }
}

rawContent {
    head {
        stylesheets >
        stylesheets {
            default = Neos.Fusion:Template {
                templatePath = 'resource://Neos.Neos/Private/Templates/RawContentMode/Page/Default.html'
                sectionName = 'headerIncludes'
            }
            tone = Ttree.Neos.Tone:TonePreviewStyleSheet {
                @position = 'end'
            }
        }
    }
}

tone = Neos.Neos:Page {
    head {
        stylesheets = Neos.Fusion:Template {
            templatePath = 'resource://Ttree.Neos.Tone/Private/Templates/RawContentMode/Page/Default.html'
            sectionName = 'headerIncludes'
        }
    }

    bodyTag.attributes.class = 'neos-raw-content-mode'

    body {
        templatePath = 'resource://Ttree.Neos.Tone/Private/Templates/RawContentMode/Page/Default.html'
        sectionName = 'body'

        allContentCollections = Neos.Neos:PrimaryContent {
            nodePath = '.'
            default.renderer = Ttree.Neos.Tone:TonePreview
        }
    }
}
